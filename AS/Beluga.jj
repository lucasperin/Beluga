/*************************************************************************
Fonte utilizada em cima do livro "Como construir um compilador"
Autor: Márcio Eduardo Delamaro

Alunos:
Lucas Perin
Gabriel Gaspar
************************************************************************/
options {
  STATIC = false;
  DEBUG_LOOKAHEAD = true;
}

PARSER_BEGIN(langB)
package parser;
import java.io.*;
 

public class langB {
final static String Version = "Beluga Compiler - Version 1.0 - 2013";
int contParseError = 0;           // contador de erros sintaticos


// Define o metodo "main" da classe langB.  
  public  static void main(String args[]) throws ParseException 
  {
  boolean debug = false;

    String filename = ""; // nome do arquivo a ser analisado
    langB parser;     // analisador lexico/sintatico
    int i;
    boolean ms = false;

    System.out.println(Version);
    // le os parametros passados para o compilador
    for (i = 0; i < args.length - 1; i++)
    {
    if (args[i].equals("-debug_AS") )
            debug = true;
    else
        {
            System.out.println("Usage is: java langB [-debug_AS] inputfile");
            System.exit(0);
        }
    }

    if (args[i].equals("-"))
    {       // le da entrada padrao      
        System.out.println("Lendo da entrada padrao . . .");
        parser = new langB(System.in); // cria AS
     }
     else 
    {       // le do arquivo
        filename = args[args.length-1];  
        System.out.println("Lendo do arquivo " + filename + " . . .");
        try {  // cria AS
            parser = new langB(new java.io.FileInputStream(filename));
        } 
    catch (java.io.FileNotFoundException e) {
               System.out.println("Arquivo " + filename + " nao encontrado.");
        return;
        }
    } 

    if (! debug) parser.disable_tracing(); // desabilita verbose do AS

    try {
        parser.program();   // chama o metodo que faz a analise
    }
    catch (ParseException e)
    {
    System.err.println(e.getMessage());
    parser.contParseError = 1;   // nao existe recuperacao de erros
    }
    finally {
        System.out.println(parser.token_source.foundLexError() +
                                " Erros lexicos encontrados");
        System.out.println(parser.contParseError + " Erros sintaticos encontrados");
     }

}  // main

static public String im(int x)
{
int k;
String s;
   s = tokenImage[x];
   k = s.lastIndexOf("\"");
   try {s = s.substring(1,k);}
   catch (StringIndexOutOfBoundsException e)
   {}
   return s;
}

}  // langB

PARSER_END(langB)

TOKEN_MGR_DECLS :
{
int countLexError = 0;

public int foundLexError()
{
   return countLexError;
}

}


/* Espacos a serem desprezados no inicio de cada token */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SKIP :
{
  "/*" : multilinecomment
}

SKIP :
{
  "//" : singlelinecomment
}

<multilinecomment> SKIP:
{
   "*/" : DEFAULT
|  <~[]>
}

<singlelinecomment> SKIP:
{
   <["\n","\r"]> : DEFAULT
|   <~[]>
}


/* Palavras reservadas */

TOKEN :
{
  < BREAK: "parar" > 
| < CLASS: "classe" >
| < CONSTRUCTOR: "construtor" >
| < ELSE: "senao" >
| < EXTENDS: "herda" >
| < FOR: "para" >
| < IF: "se" >
| < THEN: "entao" >
| < INT: "inteiro" >
| < NEW: "novo" >
| < PRINT: "imprimir" >
| < READ: "ler" >
| < RETURN: "retornar" >
| < STRING: "texto" >
| < SUPER: "super" >
| < BOOLEAN: "cara_coroa" >
| < WHILE: "enquanto" >
| < SWITCH: "trocar" >
| < CASE: "caso" >
| < DEFAULT_CASE: "padrao" >
| < VAR: "variavel" >
| < VOID: "vazio" >
}

/* constantes */

TOKEN :
{
  < int_constant:(  // n�meros decimais, octais, hexadecimais ou bin�rios
                  (["0"-"9"] (["0"-"9"])* ) | 
                  (["0"-"7"] (["0"-"7"])* ["o", "O"] ) |
                  (["0"-"9"] (["0"-"7","A"-"F","a"-"f"])* ["h", "H"] ) |
                  (["0"-"1"] (["0"-"1"])* ["b", "B"])
                 ) >
|
  < string_constant:    // constante string como "abcd bcda"
      "\""( ~["\"","\n","\r"])* "\"" >
|
  < null_constant: "nulo" > // constante null
|
  < boolean_constant: "cara" | "coroa" > // constante booleana
}

/* Identificadores */

TOKEN :
{
  < IDENT: <LETTER> (<LETTER>|<DIGIT>|<UNDERSCORE>)* >
|
  < #LETTER:["A"-"Z","a"-"z"] >
|
  < #DIGIT:["0"-"9"] >
|
  < #UNDERSCORE:["_"] >
}

/* Simbolos especiais */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < DDOT: ":" >
| < QUESTIONMARK: "?" >
}

/* Operadores */

TOKEN :
{
  < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NEQ: "!=" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < REM: "%" >
| < OR: "|" >
| < AND: "&" >
}

/* Trata os erros l�xicos */
SPECIAL_TOKEN :
{
<INVALID_LEXICAL: 
(~ ["a"-"z", "A"-"Z", "_", ":", "?",
  "0"-"9",
  "\"",
  "(",
  ")",
  "[",
  "]",
  "{",
  "}",
  ";",
  ",",
  ".",
  "=",
  ">",
  "<",
  "!",
  "+",
  "-",
  "*",
  "/",
  "%",
  "|",
  "&",
  " ",
  "\t",
  "\n",
  "\r",
  "\f"
])+>
   {
    System.err.println("Na linha [" + input_stream.getEndLine() + 
                           "] - texto invalido encontrado: " + image);
    countLexError++;
   }
|
   <INVALID_CONST: 
   "\"" (~ ["\n","\r","\""])* ["\n","\r"]>
   {
    System.err.println("Na linha [" + input_stream.getEndLine() + 
                           "] - Constante texto possui \\n: " + image);
    countLexError++;
   }

}



void program() :
{
}
{
    [ classlist() ] <EOF>
}

void classlist(): 
{
}
{
   classdecl() [ classlist() ]
}


void classdecl():
{
}
{
   <CLASS>  <IDENT> [ <EXTENDS> <IDENT> ] classbody() 
}

void classbody():
{                         
}
{
   <LBRACE>
      [classlist()]
      (vardecl() <SEMICOLON>)* // OTIMIZACAO: Removido lookahead, adicionado "variavel" o inicio de vardecl
      (constructdecl())*
      (methoddecl())* 
   <RBRACE>
}

void vardecl():
{
}
{
  <VAR> (<INT> |  <STRING> | <IDENT> | <BOOLEAN > ) 
   <IDENT> ( <LBRACKET> <RBRACKET>)* 
  (<COMMA> <IDENT> ( <LBRACKET> <RBRACKET>)* )*  
}


void constructdecl():
{
}
{
    <CONSTRUCTOR> methodbody()
}

void methoddecl():
{
}
{
   (<INT> |  <STRING> | <IDENT> | <BOOLEAN> | < VOID > ) (<LBRACKET> <RBRACKET>)* 
   <IDENT> methodbody()
}


void methodbody():
{
}
{
    <LPAREN> paramlist() <RPAREN> statement()
}

void paramlist():
{
}
{
   [
    (<INT> |  <STRING> | <IDENT> | <BOOLEAN>) <IDENT> (<LBRACKET> <RBRACKET>)* 
    [<COMMA> paramlist()]
   ]
}

void statement(): // (comando)
{
}
{
    //LOOKAHEAD(2) OTIMIZACAO: vardecl inicia com "variavel"
    vardecl() <SEMICOLON>
|
    atribstat() <SEMICOLON>  //comando_atribuicao
|
    printstat() <SEMICOLON> //comando_imprimir
|
    readstat() <SEMICOLON>  //comando_ler
|
    returnstat() <SEMICOLON> //comando_retorno
|
    superstat() <SEMICOLON> //comando_super
|
    ifstat() //comando_se_entao
|
    forstat() //comando_for
|
	whilestat() //comando_enquanto TODO
|
	switchstat() //comando_trocar_caso
| 
    <LBRACE> statlist() <RBRACE> //lista_comandos
|
    <BREAK> <SEMICOLON>
|
    <SEMICOLON>
}


void atribstat(): //comando_atribuicao
{
}
{
    lvalue() <ASSIGN> ( alocexpression() | expression() [conditional()])
}

void conditional() :
{
}
{
	<QUESTIONMARK> ( expression() | alocexpression() ) <DDOT> ( expression() | alocexpression() )
}  

void printstat():
{
}
{
    <PRINT> expression()
}

void readstat():
{
}
{
    <READ> lvalue()
}


void returnstat():
{
}
{  
    <RETURN> [expression()]
}


void superstat():
{
}
{
    <SUPER> <LPAREN> arglist() <RPAREN>
}

void ifstat():
{
}
{
    <IF> <LPAREN> expression() <RPAREN> <THEN> statement()
    [ LOOKAHEAD(1) <ELSE> statement()]
}


void forstat():
{
}
{
    <FOR> <LPAREN> [atribstat()] <SEMICOLON>
                   [expression()] <SEMICOLON>
                   [atribstat()] <RPAREN>
                    statement()
}


void whilestat():
{
}
{
	< WHILE > <LPAREN> expression()	< RPAREN > statement()
}

void switchstat():
{
}
{
	< SWITCH > < LPAREN > expression() < RPAREN > < LBRACE > caselist() < RBRACE >
}

void caselist():
{
}
{
	casestat() [caselist()]|	<DEFAULT_CASE> <DDOT> statement()
}

void casestat():
{
}
{
	< CASE > factor() < DDOT > statement()
}

void  statlist() :
{
}
{
   statement() [statlist()]
}

void lvalue() :
{
}
{
   <IDENT> (
           <LBRACKET> expression() <RBRACKET> |
           <DOT> <IDENT> [<LPAREN> arglist() <RPAREN>]
         )*
}

void alocexpression() :
{
}
{
    <NEW> ( 
             LOOKAHEAD(2) <IDENT> <LPAREN> arglist() <RPAREN> | 
             ( <INT> | <STRING> | <IDENT> | < BOOLEAN >)  
             (<LBRACKET> expression() <RBRACKET>)+
          )
} 

void expression() :
{
}
{
    numexpr() [( <LT> | <GT> | <LE> | <GE> | <EQ> | <NEQ> |< OR >|< AND >) numexpr()]
}


void numexpr():
{
}
{
    term() ((<PLUS> | <MINUS>) term())*
}

void term():
{
}
{
    unaryexpr() ((<STAR> | <SLASH>| <REM>) unaryexpr())*
}

void unaryexpr() :
{
}
{
   [(<PLUS> | <MINUS>)] factor()
}


void factor():
{
}
{

    
    ( 
       <int_constant> | 
       <string_constant> | 
       <null_constant> |
       <boolean_constant> |
       lvalue() |
       <LPAREN> expression() <RPAREN>)
}

void arglist():
{
}
{
    [expression() (<COMMA> expression())*]
}
